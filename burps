#!/usr/bin/perl -w

use strict;
use File::Basename;
use lib dirname($0) . '/lib';
use BURPS;
use YAML::XS;
use Getopt::Long;
#use Data::Dump qw/dd/;

my %actions = (
    projects => {
        run   => \&print_projects,
        descr => 'Print projects list',
    },
    fetch => {
        run => \&fetch,
        descr => 'Fetch commits from remote git repository',
    },
    tar  => {
        run   => \&tar,
        descr => 'Create source tarball',
    },
    rpmspec => {
        run => \&rpmspec,
        descr => 'Create rpm spec file',
    },
    rpm => {
        run => sub { build_script('rpm', @_) },
        descr => 'Build an rpm package',
    },
    srpm => {
        run => sub { build_script('srpm', @_) },
        descr => 'Create source rpm file',
    },
    'deb-src' => {
        run => sub { build_script('deb_src', @_) },
        descr => 'Create debian source package',
    },
    'deb' => {
        run => sub { build_script('deb', @_) },
        descr => 'Create debian package',
    },
    build => {
        run => sub { build_script('build', @_) },
        descr => 'Build project with a custom build script',
    },
    pkg => {
        run => \&pkg,
        descr => 'Build a package',
    },
    publish => {
        run => \&publish,
        descr => 'Publish a package',
    },
    showconf => {
        run => \&show_conf,
        descr => 'Show configuration',
    },
    usage => {
        run => \&usage,
        descr => 'Show usage information for an action',
        no_config => 1,
    },
    '--help' => {
        run => \&usage,
        no_config => 1,
    },
);

sub usage {
    if ($_[1] && $actions{$_[1]} && $actions{$_[1]}->{descr}) {
        system('man', "burps-$_[1]");
    } else {
        print STDERR "$0 <action> [options]\n";
        print STDERR "$0 usage [action]\n\n";
        print STDERR "Available actions:\n";
        my @actions = grep { $actions{$_}->{descr} } keys %actions;
        print STDERR map { " - $_ : $actions{$_}->{descr}\n" } @actions;
        print STDERR "\nSee '$0 usage <action>' for usage informations\n";
    }
    exit 0;
}
sub usageexit {
    my $cmd = shift;
    print STDERR "Incorrect argument(s).\n";
    print STDERR "See '$0 usage $cmd' for usage informations\n";
    exit 1;
}

sub set_options {
    my @options = qw(distribution=s version=s tag-gpg-id=s@ commit-gpg-id=s@
                     projects-dir=s git-clone-dir=s git-hash=s output-dir=s
                     compress_tar=s pkg-rel=s timestamp=i fetch!
                     gpg-keyring=s gpg-keyring-dir=s gpg-args=s gpg-bin=s
                     sysconf-file=s debsign-keyid=s use-pbuilder!
                     step=s target=s@ publish-src-dir=s debug!);
    my %val;
    Getopt::Long::GetOptionsFromArray(\@_, \%val, @options) || exit 1;
    foreach my $k (keys %val) {
        if ($k eq 'step') {
            $BURPS::config->{step} = $val{$k};
            next;
        }
        my $l = $k;
        $l =~ s/-/_/g;
        $BURPS::config->{run}{$l} = $val{$k};
    }
    BURPS::load_system_config(@_);
    return $BURPS::config->{run}{args} = \@_;
}

sub show_conf {
    shift;
    my $args = set_options(@_);
    if (@$args == 0) {
        print YAML::XS::Dump($BURPS::config);
        return;
    }
    my $project = shift @$args;
    BURPS::valid_project($project);
    my $r = @$args ? BURPS::project_config($project,
                        @$args == 1 ? $args->[0] : \@$args)
                : $BURPS::config->{projects}{$project};
    BURPS::exit_error "Undefined" unless defined $r;
    print ref $r ? YAML::XS::Dump($r) : "$r\n";
}

sub fetch {
    shift;
    $BURPS::config->{run}{fetch} = 1;
    my @l = @_ ? @_ : (BURPS::projectslist());
    foreach my $project (@l) {
        BURPS::valid_project($project);
        if (BURPS::project_config($project, 'git_url')) {
            print "Fetching commits for $project\n";
            BURPS::git_clone_fetch_chdir($project);
        } else {
            print "Skipping $project\n";
        }
    }
}

sub tar {
    usageexit($_[0]) unless @_ >= 2;
    shift;
    my $args = set_options(@_);
    usageexit('tar') unless @$args == 1;
    BURPS::maketar($args->[0]);
}

sub rpmspec {
    usageexit($_[0]) unless @_ >= 2;
    shift;
    my $args = set_options(@_);
    usageexit('rpmspec') unless @$args == 1;
    BURPS::rpmspec($args->[0]);
}

sub print_projects {
    usageexit($_[0]) unless @_ == 1;
    print join("\n", BURPS::projectslist()), "\n";
}

sub build_script {
    my $script_name = shift;
    usageexit($_[0]) unless @_ >= 2;
    my $cmd = shift;
    my $args = set_options(@_);
    usageexit($cmd) unless @$args == 1;
    BURPS::build_run($args->[0], $script_name);
}

sub pkg {
    usageexit($_[0]) unless @_ >= 2;
    my $cmd = shift;
    my $args = set_options(@_);
    usageexit($cmd) unless @$args == 1;
    BURPS::build_pkg($args->[0]);
}

sub publish {
    usageexit($_[0]) unless @_ >= 2;
    my $cmd = shift;
    my $args = set_options(@_);
    usageexit($cmd) unless @$args == 1;
    BURPS::publish($args->[0]);
}

if (@ARGV == 0 || !$actions{$ARGV[0]}) {
    usage();
    exit 1;
}
usage('usage', $ARGV[0]) if grep { $_ eq '--help' } @ARGV[1..(@ARGV - 1)];
BURPS::load_config unless $actions{$ARGV[0]}->{no_config};
$actions{$ARGV[0]}->{run}->(@ARGV);

# vim: expandtab sw=4
