#!/usr/bin/perl -w

use strict;
use File::Basename;
use lib dirname($0) . '/lib';
use MkPkg;
use YAML::XS;
use Getopt::Long;
#use Data::Dump qw/dd/;

my %actions = (
    projects => {
        run   => \&print_projects,
        descr => 'Print projects list',
    },
    fetch => {
        run => \&fetch,
        descr => 'Fetch commits from remote git repository',
    },
    tar  => {
        run   => \&tar,
        descr => 'Create source tarball',
    },
    rpmspec => {
        run => \&rpmspec,
        descr => 'Create rpm spec file',
    },
    rpm => {
        run => \&makerpm,
        descr => 'Build an rpm package',
    },
    srpm => {
        run => \&makerpm,
        descr => 'Create source rpm file',
    },
    'deb-src' => {
        run => sub { build_script('deb_src', @_) },
        descr => 'Create debian source package',
    },
    'deb' => {
        run => sub { build_script('deb', @_) },
        descr => 'Create debian package',
    },
    build => {
        run => sub { build_script('build', @_) },
        descr => 'Build project with a custom build script',
    },
    showconf => {
        run => \&show_conf,
        descr => 'Show configuration',
    },
    usage => {
        run => \&usage,
        descr => 'Show usage information for an action',
    },
);

sub usage {
    if ($_[1] && $actions{$_[1]}) {
        system('man', "mkpkg-$_[1]");
    } else {
        print STDERR "$0 <action> [options]\n";
        print STDERR "$0 usage [action]\n\n";
        print STDERR "Available actions:\n";
        print STDERR map { " - $_ : $actions{$_}->{descr}\n" } keys %actions;
        print STDERR "\nSee '$0 usage <action>' for usage informations\n";
    }
}
sub usageexit {
    my $cmd = shift;
    print STDERR "Incorrect argument(s).\n";
    print STDERR "See '$0 usage $cmd' for usage informations\n";
    exit 1;
}

sub set_options {
    my @options = qw(distribution=s version=s tag-gpg-id=s@ commit-gpg-id=s@
                     projects-dir=s git-clone-dir=s git-hash=s output-dir=s
                     compress_tar=s pkg-rel=s timestamp=i fetch!
                     gpg-keyring=s gpg-keyring-dir=s gpg-args=s gpg-bin=s
                     sysconf-file=s debsign-keyid=s use-pbuilder!);
    my %val;
    Getopt::Long::GetOptionsFromArray(\@_, \%val, @options) || exit 1;
    foreach my $k (keys %val) {
        my $l = $k;
        $l =~ s/-/_/g;
        $MkPkg::config->{run}{$l} = $val{$k};
    }
    MkPkg::load_system_config(@_);
    return $MkPkg::config->{run}{args} = \@_;
}

sub show_conf {
    shift;
    my $args = set_options(@_);
    if (@$args == 0) {
        print YAML::XS::Dump($MkPkg::config);
        return;
    }
    my $project = shift @$args;
    MkPkg::valid_project($project);
    my $r = @$args ? MkPkg::project_config($project,
                        @$args == 1 ? $args->[0] : \@$args)
                : $MkPkg::config->{projects}{$project};
    MkPkg::exit_error "Undefined" unless defined $r;
    print ref $r ? YAML::XS::Dump($r) : "$r\n";
}

sub fetch {
    shift;
    $MkPkg::config->{run}{fetch} = 1;
    my @l = @_ ? @_ : (MkPkg::projectslist());
    foreach my $project (@l) {
        MkPkg::valid_project($project);
        print "Fetching commits for $project\n";
        MkPkg::git_clone_fetch_chdir($project);
    }
}

sub tar {
    usageexit($_[0]) unless @_ >= 2;
    shift;
    my $args = set_options(@_);
    usageexit('tar') unless @$args == 1;
    MkPkg::maketar($args->[0]);
}

sub rpmspec {
    usageexit($_[0]) unless @_ >= 2;
    shift;
    my $args = set_options(@_);
    usageexit('rpmspec') unless @$args == 1;
    MkPkg::rpmspec($args->[0]);
}

sub print_projects {
    usageexit($_[0]) unless @_ == 1;
    print join("\n", MkPkg::projectslist()), "\n";
}

sub makerpm {
    usageexit($_[0]) unless @_ >= 2;
    my $cmd = shift;
    my $args = set_options(@_);
    usageexit($cmd) unless @$args == 1;
    my %actions = ( srpm => '-bs', rpm  => '-ba' );
    MkPkg::rpmbuild($args->[0], $actions{$cmd});
}

sub build_script {
    my $script_name = shift;
    usageexit($_[0]) unless @_ >= 2;
    my $cmd = shift;
    my $args = set_options(@_);
    usageexit($cmd) unless @$args == 1;
    MkPkg::build_run($args->[0], $script_name);
}

if (@ARGV == 0 || !$actions{$ARGV[0]}) {
    usage();
    exit 1;
}
MkPkg::load_config;
$actions{$ARGV[0]}->{run}->(@ARGV);

